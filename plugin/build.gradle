apply plugin: 'maven'
apply plugin: 'maven-publish'

configurations {
  maven_local

  junit
}

dependencies {
  compile gradleApi()

  testCompile gradleTestKit()
  testCompile project(':test-common')

  junit 'junit:junit:4.12'
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId rootProject.group
      artifactId 'java9c'
      version rootProject.version

      from components.java
    }
  }
}

test.dependsOn configurations.junit
test.doFirst {
  systemProperty("jonnyzzz_junit_jar", configurations.junit.asPath)
}

final File mock = file("$buildDir/artifact-marker")
task artifactMarker() {
  dependsOn publishToMavenLocal
  doLast {
    mock << "hello"
  }
}

artifacts {
  maven_local(mock) {
    builtBy artifactMarker
  }
}


if (!"${rootProject.version}".toLowerCase().contains("SNAPSHOT".toLowerCase())) {

  apply plugin: "com.gradle.plugin-publish"
  pluginBundle {
    website = 'https://jonnyzzz.com'
    vcsUrl = 'https://github.com/jonnyzzz/gradle-java9c'
    description = 'Checks runtime classpaths to detect same packages used in different classpath entries. Detects possible Java 9 modules migration problems'
    tags = ['java9', 'jigsaw', 'split packages', 'package', 'module']

    plugins {
      java9c {
        id = 'org.jonnyzzz.java9c'
        displayName = 'org.jonnyzzz.java9c'
      }
    }
  }
}
